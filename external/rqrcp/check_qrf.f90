!     M IS NUMBER OF ROWS IN A
!     N IS NUMBER OF COLUMNS IN A
!     K IS THE APPROXIMATE RANK
!     A IS INITIAL MATRIX  (INPUT ONLY)
!     DESCA IS DESCRIPTOR OF A
!     B IS OUTPUT OF QR WITH PIVOTING, THE HOUSEHOLDER VECTORS ARE STORED IN LOWER PART OF B(1:M,1:K) (INPUT ONLY)
!     DESCB IS DESCRIPTOR OF B
!     C IS A WORK ARRAY, STORE THE RESULT OF PRODUCT OF A BLOCK OF HOUSEHOLDER REFLECTORS AND TRAILING MATRIX (WORKSPACE)
!     DESCC IS DESCRIPTOR OF C
!     TAU IS THE COEFFICIENTS OF HOUSEHOLDER VECTORS
!     WORK IS WORK ARRAY
!     LWORK IS SIZE OF WORK ARRAY

!     A, B, C ARE THE SAME SIZE

!	  AUTHOR: JIANWEI XIAO AND JULIEN LANGOU

DOUBLE PRECISION FUNCTION CHECK_QRF( M, N, K, A, DESCA, B, DESCB, C, DESCC, TAU, WORK, LWORK )

IMPLICIT NONE

INTEGER            M, N, K, INFO, LWORK
INTEGER            DESCA( * ), DESCB( * ), DESCC( * )
DOUBLE PRECISION   A( * ), B( * ), C( * ), TAU( * ), WORK( * )
DOUBLE PRECISION   ANORM, CNORM

DOUBLE PRECISION   ZERO, MONE, ONE
PARAMETER          ( ZERO = 0.0D0, MONE = -1.0D0, ONE = 1.0D0 )

EXTERNAL           PDGEMM

DOUBLE PRECISION   PDLANGE
EXTERNAL           PDLANGE

ANORM = PDLANGE( 'F', M, N, A, 1, 1, DESCA, WORK )

CALL PDLASET( 'ALL', M, N, ZERO, ZERO, C, 1, 1, DESCC )
CALL PDLACPY( 'U', K, K, B, 1, 1, DESCB, C, 1, 1, DESCC )
CALL PDLACPY( 'ALL', M, N - K, B, 1, K+1, DESCB, C, 1, K+1, &
 DESCC)

CALL PDORMQR( 'L', 'N', M, N, K, B, 1, 1, DESCB, TAU, C, 1, 1, &
   DESCC, WORK, LWORK, INFO)

CALL PDGEADD( 'N', M, N, MONE, A, 1, 1, DESCA, ONE, C, 1, 1,&
   DESCC )

CNORM = PDLANGE( 'F', M, N, C, 1, 1, DESCC, WORK )

! CHECK_QRF SHOULD BE ZERO
CHECK_QRF = CNORM / ANORM

END
